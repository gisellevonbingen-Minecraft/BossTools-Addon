buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "maven-publish"
apply plugin: "org.spongepowered.mixin"

version = "${mc_version}-${mod_version}"
group = "gisellevonbingen.beyond_earth_giselle_addon"
archivesBaseName = "Beyond-Earth-Giselle-Addon"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println "Java: ${System.getProperty "java.version"}, JVM: ${System.getProperty "java.vm.version"} (${System.getProperty "java.vendor"}), Arch: ${System.getProperty "os.arch"}"

minecraft {
    mappings channel: "official", version: "${mc_version}"

    // accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "beyond_earth_giselle_addon"
            arg "-mixin.config=beyond_earth_giselle_addon.mixin.json"

            mods {
                beyond_earth_giselle_addon {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "beyond_earth_giselle_addon"
            arg "-mixin.config=beyond_earth_giselle_addon.mixin.json"

            mods {
                beyond_earth_giselle_addon {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "beyond_earth_giselle_addon"

            mods {
                beyond_earth_giselle_addon {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            args "--mod", "beyond_earth_giselle_addon", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                beyond_earth_giselle_addon {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }

repositories {
    maven {
        name "JEI"
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "ModMaven"
        url "https://modmaven.dev"
    }
    maven {
        name "CurseForge"
        url "https://www.cursemaven.com"
    }
    maven {
        name "CraftTweaker"
        url "https://maven.blamejared.com"
    }
    maven {
        name "Local"
        url uri("${projectDir}/libs")
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Mixin
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // JEI
    implementation fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

    // JER
    implementation fg.deobf("curse.maven:just-enough-resources-jer-240630:${jer_file}")

    // The One Probe
    implementation fg.deobf("curse.maven:the-one-probe-245211:${top_file}")

    // Jade
    implementation fg.deobf("curse.maven:jade-324717:${jade_file}")

    // Tinkers Construct
    implementation fg.deobf("slimeknights.mantle:Mantle:${mc_version}-${mantle_version}")
    implementation fg.deobf("slimeknights.tconstruct:TConstruct:${mc_version}-${tconstruct_version}")

    // Refined Storage
    // compileOnly fg.deobf("curse.maven:refined-storage-243076:${refined_storage_file}")

    // JAOPCA
    compileOnly fg.deobf("thelm:jaopca:${mc_version}-${jaopca_version}:api")

    // Curios
    implementation fg.deobf("curse.maven:curios-309927:${curios_file}")

    // Beyond Earth
    implementation fg.deobf("net.mrscauthd:beyond_earth:${mc_version}-${beyond_earth_version}")
    
    // Immersive Engineering
    compileOnly fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:${mc_version}-${immersive_engineering_version}:api")

    // Mekanism
    implementation fg.deobf("mekanism:Mekanism:${mc_version}-${mekanism_version}")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mc_version}-${mekanism_version}:additions")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mc_version}-${mekanism_version}:generators")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mc_version}-${mekanism_version}:tools")

    // Thermal Expansion
    implementation fg.deobf("curse.maven:cofh-core-69162:${cofh_core_file}")
    implementation fg.deobf("curse.maven:thermal-foundation-222880:${thermal_foundation_file}")
    implementation fg.deobf("curse.maven:thermal-expansion-69163:${thermal_expansion_file}")
    
    // Craft Tweaker
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${mc_version}:${crafttweaker_version}")
    implementation fg.deobf("com.blamejared.jeitweaker:JEITweaker-${mc_version}:${jeitweaker_version}")
}

mixin {
    add sourceSets.main, "beyond_earth_giselle_addon.mixin-refmap.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "beyond_earth_giselle_addon",
                "Specification-Vendor"    : "gisellevonbingen",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "gisellevonbingen",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs" : "beyond_earth_giselle_addon.mixin.json"
        ])
    }
}

jar.finalizedBy("reobfJar")